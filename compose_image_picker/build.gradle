plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 34

    defaultConfig {
        minSdk 26
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = compose_compiler_version
    }

    publishing {
        singleVariant("release") {
            withJavadocJar()
            withSourcesJar()
        }
    }
    namespace 'com.huhx.picker'
}

publishing {
    publications {
        release(MavenPublication) {
            pom {
                name = 'Image Picker Compose'
                description = 'An Image Picker Library for Jetpack Compose'
                url = 'https://github.com/huhx/compose_image_picker'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'huhx'
                        name = 'hongxiang'
                        email = 'gohuhx@gmail.com'
                    }
                }

                scm {
                    connection = 'https://github.com/huhx/compose_image_picker.git'
                    developerConnection = 'https://github.com/huhx/compose_image_picker.git'
                    url = 'https://github.com/huhx/compose_image_picker'
                }
            }

            groupId "io.github.huhx"
            artifactId "compose-image-picker"
            version "1.0.10"

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.release
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.ui:ui"

    implementation "com.google.accompanist:accompanist-permissions:0.33.2-alpha"

    implementation 'androidx.navigation:navigation-compose:2.7.6'
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-video:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"

    implementation "androidx.media3:media3-exoplayer:1.2.0"
    implementation "androidx.media3:media3-ui:1.2.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}